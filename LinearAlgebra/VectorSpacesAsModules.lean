import Mathlib.Logic.Nontrivial
import Mathlib.Logic.Basic
import Mathlib.Init.ZeroOne
import Mathlib.Init.Data.Int.Basic
import Mathlib.Init.Algebra.Order
import Std.Data.Rat
import Mathlib.Tactic.Convert
import Mathlib.Data.Rat.Init

/-!
Math:

* Algebra:
  An algebra is a system A = [P, F] in which

    (i) P = {Sáµ¢} is a family of nonempty sets Sáµ¢ of different types of
    elements, each called a phylum of the algebra A. The phyla are
    indexed by some set I.
    
    (ii) F = {fâ‚} is a set of finitary operations, where each fâ‚ is a map
        fâ‚ : Sáµ¢â‚â‚,â‚â‚ Ã— â‹¯ Ã— Sáµ¢â‚â‚™â‚â‚â‚,â‚â‚ â†’ Sáµ£â‚â‚â‚,
      where n(a) is a nonnegative integer, iâ‚ : k â†¦ i(k,a) is a map from
      {1, â‹¯ , n} to I, and r(a) âˆˆ I. The operations fâ‚ are indexed by some
      set Î©.
  
  A heterogeneous algebra is an algebra with more than one phylum.

  A homogeneous algebra is an algebra with exactly one phylum.

* Magma:
  A magma is a set M with a binary operation â¬ : M Ã— M â†’ M. Magmas are used to
  define semigroups.

  An additive magma is a magma for which we think of the binary operation,
  denote +, as addition in some sense.

  A magma with zero is a magma M for which there exists an element 0 âˆˆ M such
  that 0 â¬ x = 0 = x â¬ 0 for all x âˆˆ M.

  A unital magma is a magma M with an element 1 âˆˆ M such that 1 â¬ x = x = x â¬ 1
  for all x âˆˆ M.

  An additive unital magma is a unital magma for which we think of the binary
  operation, denoted +, as addition in some sense.

* Semigroup:
  Semigroups are associative magmas. Semigroups are used to define monoids.

  A semigroup is a set S with a binary operation â¬ : S Ã— S â†’ S such that
    (a â¬ b) â¬ c = a â¬ (b â¬ c)
  for all a, b, c âˆˆ S.

  An additive semigroup is a semigroup where we think of the binary
  operation, denoted +, as addition in some sense.

  A commutative semigroup is a semigroup such that
    a â¬ b = b â¬ a
  for all a, b âˆˆ S.

  An additive, commutative semigroup is an additive semigroup such that
    a + b = b + a
  for all a, b âˆˆ S.

* Monoid
  A monoid is a unital semigroup. Monoids are used to define groups.

  A monoid is a set M with a binary operation â¬ : M Ã— M â†’ M such that the
  following hold:

  (i) We have
    (a â¬ b) â¬ c = a â¬ (b â¬ c)
  for all a, b, c âˆˆ M

  (ii) There exists e âˆˆ M such that e â¬ a = a = a â¬ e for all a âˆˆ M.
  
  An additive monoid is a monoid in which we think of the binary operation,
  denoted +, as addition in some sense.

  A commutative monoid is a monoid such that a â¬ b = b â¬ a for all a, b âˆˆ M.

  An additive commutative monoid is a commutative monoid in which we think of
  the binary operation, denoted +, as addition in some sense.

* Group
  A group is a semigroup with inverses.

* Semiring
  A semiring is a ring without inverses.

* Ring
  A ring is an abelian group under addition, a monoid under multiplication
  and has distributive laws.

* Field
  Fields generalize groups to things we are used to working with, e.g. â„š and â„.

* Vector Space
  A vector space is an additive commutative group under addition acted upon by a
  field.
-/

open Function


section HelpfulTypeclasses

universe u

/-- `Inv` is the typeclass for types with an inversion operation. -/
@[to_additive, notation_class]
class Inv (I : Type u) where
  /-- Invert an element of Î±. -/
  inv : I â†’ I

@[inherit_doc]
postfix:max "â»Â¹" => Inv.inv

/-- `Distrib` is the typeclass for types with addition and multiplication operations
      for which multiplication distributes over addition. -/
class Distrib (D : Type _) extends Mul D, Add D where
  protected left_distrib : âˆ€ a b c : D, a * (b + c) = a * b + a * c
  protected right_distrib : âˆ€ a b c : D, (a + b) * c = a * c + b * c

end HelpfulTypeclasses


section Algebra

/-- `HVAdd` is the notation typeclass for heterogeneous addition. This is
      used to define a heterogeneous algebra with finitary operation `+áµ¥`. -/
class HVAdd (Î± : Type u) (Î² : Type v) (Î³ : outParam (Type w)) where
  hAdd : Î± â†’ Î² â†’ Î³

/-- `HSMul` is the notation typeclass for heterogeneous scalar multiplication.
      This enables the notation `a â€¢ b : Î³`, where `a : Î±` and `b : Î²`, and
      is used to define a heterogeneous algebra with a finitary operation `â€¢`. -/
class HSMul (Î± : Type u) (Î² : Type v) (Î³ : outParam (Type w)) where
  hSMul : Î± â†’ Î² â†’ Î³

attribute [notation_class smul Simps.copySecond] HSMul
attribute [notation_class nsmul Simps.nsmulArgs] HSMul
attribute [notation_class zsmul Simps.zsmulArgs] HSMul

/-- `VAdd` is the typeclass for the `+áµ¥` notation. -/
class VAdd (G : Type _) (P : Type _) where
  vadd : G â†’ P â†’ P

/-- `SMul` is the typeclass for types with a scalar multiplication
      operation `â€¢`. -/
@[to_additive (attr := ext)]
class SMul (S : Type _) (Î± : Type _) where
  smul : S â†’ Î± â†’ Î±

infixr:73 " â€¢ " => HSMul.hSMul

attribute [to_additive existing] Mul Div HMul instHMul HDiv instHDiv HSMul
attribute [to_additive (reorder := 1 2) SMul] Pow
attribute [to_additive (reorder := 1 2)] HPow
attribute [to_additive existing (reorder := 1 2, 5 6) hSMul] HPow.hPow
attribute [to_additive existing (reorder := 1 2, 5 6) smul] Pow.pow

@[to_additive (attr := default_instance)]
instance instHSMul [SMul Î± Î²] : HSMul Î± Î² Î² where
  hSMul := SMul.smul

attribute [to_additive existing (reorder := 1 2)] instHPow

end Algebra


section Magma

/-- `MulOneClass` is the typeclass for unital magmas. -/
class MulOneClass (M : Type u) extends One M, Mul M where
  one_mul : âˆ€ a : M, 1 * a = a
  mul_one : âˆ€ a : M, a * 1 = a 

/-- `AddZeroClass` is the typeclass for additive, unital magmas. -/
class AddZeroClass (M : Type u) extends Zero M, Add M where
  zero_add : âˆ€ a : M, 0 + a = a
  add_zero : âˆ€ a : M, a + 0 = a

attribute [to_additive] MulOneClass


section MulOneClass

/- Note: Missing @[ext] lemma -/

variable {M : Type u} [MulOneClass M]

@[to_additive (attr := simp)]
theorem one_mul : âˆ€ a : M, 1 * a = a :=
  MulOneClass.one_mul

@[to_additive (attr := simp)]
theorem mul_one : âˆ€ a : M, a * 1 = a :=
  MulOneClass.mul_one

end MulOneClass


/-- `MulZeroClass` is the typeclass for magmas with a zero. -/
class MulZeroClass (M : Type u) extends Mul M, Zero M where
  zero_mul : âˆ€ a : M, 0 * a = 0
  mul_zero : âˆ€ a : M, a * 0 = 0

/-- `MulZeroOneClass` is the typeclass for unital magmas (nonassociative monoids)
      with zeros. -/
class MulZeroOneClass (M : Type u) extends MulZeroClass M, MulOneClass M

end Magma


section Semigroup

/-- `Semigroup` is the typeclass for semigroups. -/
@[ext]
class Semigroup (S : Type u) extends Mul S where
  mul_assoc : âˆ€ a b c : S, a * b * c = a * (b * c)

/-- `AddSemigroup` is the typeclass for additive semigroups. -/
@[ext]
class AddSemigroup (S : Type u) extends Add S where
  add_assoc : âˆ€ a b c : S, a + b + c = a + (b + c)

attribute [to_additive] Semigroup


section

variable [Semigroup S]

@[to_additive]
theorem mul_assoc : âˆ€ a b c : S, a * b * c = a * (b * c) :=
  Semigroup.mul_assoc

end


/-- `CommSemigroup` is the typeclass for commutative semigroups. -/
@[ext]
class CommSemigroup (S : Type u) extends Semigroup S where
  mul_comm : âˆ€ a b : S, a * b = b * a

/-- `AddCommSemigroup` is the typeclass for commutative additive semigroups. -/
@[ext]
class AddCommSemigroup (S : Type u) extends AddSemigroup S where
  add_comm : âˆ€ a b : S, a + b = b + a

attribute [to_additive] CommSemigroup

/-- `SemigroupWithZero` is the typeclass for semigroups with zero. -/
class SemigroupWithZero (G : Type u) extends Semigroup G, MulZeroClass G

end Semigroup


section NaturalNumberOperations

variable {M : Type U}

/-- `npowRec` is the fundamental power operation in a monoid. -/
def npowRec [One M] [Mul M] : â„• â†’ M â†’ M
  | 0, _ => 1
  | n + 1, a => a * npowRec n a

/-- `nsmulRec` is the fundamental scalar multiplication in an
      additive monoid. -/
def nsmulRec [Zero M] [Add M] : â„• â†’ M â†’ M
  | 0, _ => 0
  | n + 1, a => a + nsmulRec n a

attribute [to_additive existing] npowRec

end NaturalNumberOperations


section Monoid

/-- `AddMonoid` is the typeclass for additive monoids. -/
class AddMonoid (M : Type u) extends AddSemigroup M, AddZeroClass M where
  nsmul : â„• â†’ M â†’ M := nsmulRec
  nsmul_zero : âˆ€ x, nsmul 0 x = 0 := by intros; rfl
  nsmul_succ : âˆ€ (n : â„•) (x), nsmul (n + 1) x = x + nsmul n x := by intros; rfl

/-- `Monoid` is the typeclass for monoids. -/
@[to_additive]
class Monoid (M : Type u) extends Semigroup M, MulOneClass M where
  npow : â„• â†’ M â†’ M := npowRec
  npow_zero : âˆ€ x, npow 0 x = 1 := by intros; rfl
  npow_succ : âˆ€ (n : â„•) (x), npow (n + 1) x = x * npow n x := by intros; rfl

attribute [to_additive existing] Monoid.toMulOneClass

@[default_instance high]
instance Monoid.Pow {M : Type _} [Monoid M] : Pow M â„• :=
  âŸ¨fun x n â†¦ Monoid.npow n xâŸ©

instance AddMonoid.SMul {M : Type _} [AddMonoid M] : SMul â„• M :=
  âŸ¨AddMonoid.nsmulâŸ©

attribute [to_additive existing SMul] Monoid.Pow


section

variable {M : Type _} [Monoid M]

@[to_additive (attr := simp) nsmul_eq_smul]
theorem npow_eq_pow (n : â„•) (x : M) : Monoid.npow n x = x ^ n :=
  rfl

@[to_additive zero_nsmul, simp]
theorem pow_zero (a : M) : a ^ 0 = 1 :=
  Monoid.npow_zero _

@[to_additive succ_nsmul]
theorem pow_succ (a : M) (n : â„•) : a ^ (n + 1) = a * a ^ n :=
  Monoid.npow_succ n a

end


section

variable {M : Type u} [Monoid M]

@[to_additive]
theorem left_inv_eq_right_inv {a b c : M} (hba : b * a = 1 ) (hac : a * c = 1) : b = c := by
  rw [â† one_mul c, â† hba, mul_assoc, hac, mul_one b]

end

/-- `AddCommMonoid` is the typeclass for additive commutative monoids. -/
class AddCommMonoid (M : Type u) extends AddMonoid M, AddCommSemigroup M

/-- `CommMonoid` is the typeclass for commutative monoids. -/
@[to_additive]
class CommMonoid (M : Type u) extends Monoid M, CommSemigroup M

attribute [to_additive existing] CommMonoid.toCommSemigroup


section Integer_Operations

/-- `zpowRec` is the fundamental scalar multiplication in a group. -/
def zpowRec {M : Type _} [One M] [Mul M] [Inv M] : â„¤ â†’ M â†’ M
  | Int.ofNat n, a => npowRec n a
  | Int.negSucc n, a => (npowRec n.succ a)â»Â¹

/-- `zsmulRec` is the fundamental scalar multiplication in an additive group. -/
def zsmulRec {M : Type _} [Zero M] [Add M] [Neg M] : â„¤ â†’ M â†’ M
  /- Multiplication by a nonnegative integer is just applying `nsmulRec`. -/
  | Int.ofNat n, a => nsmulRec n a
  /- Multiplication by a negative integer n is the negation of `nsmulRec` applied
      to -n. -/
  | Int.negSucc n, a => -nsmulRec n.succ a

attribute [to_additive existing] zpowRec

end Integer_Operations


/--
  `DivInvMonoid.div'` records what the default definition for `Div` would be,
  that is `a * bâ»Â¹`, in a class equipped with instances of both `Monoid` and
  `Inv`. This is provided as the default value for the `Div` instance
  in `DivInvMonoid`.

  `DivInvMonoid.div'` exists as a separate definition rather than
  inside `DivInvMonoid` so that the `Div` field of individual `DivInvMonoid`s
  constructed using that default value will not unfold at `.instance` transparency.
-/
def DivInvMonoid.div' {G : Type u} [Monoid G] [Inv G] (a b : G) : G :=
  a * bâ»Â¹

/--
  `DivInvMonoid` is the typeclass for monoids with an inversion operation `â»Â¹`
  and a division operation `/` satisfying `div_eq_mul_inv : âˆ€ a b, a / b = a * bâ»Â¹`. 
  (The existence of an inversion operation `â»Â¹` does not imply that `â»Â¹` is an
  involutive inversion operation, for which it is true that `aâ»Â¹â»Â¹ = a`.)

  The default for `div` is such that `a / b = a * bâ»Â¹` holds by definition.
-/
class DivInvMonoid (G : Type u) extends Monoid G, Inv G, Div G where
  div := DivInvMonoid.div'
  div_eq_mul_inv : âˆ€ a b : G, a / b = a * bâ»Â¹ := by intros; rfl
  zpow : â„¤ â†’ G â†’ G := zpowRec
  zpow_zero' : âˆ€ a : G, zpow 0 a = 1 := by intros; rfl
  zpow_succ' (n : â„•) (a : G) : zpow (Int.ofNat n.succ) a = a * zpow (Int.ofNat n) a := by
    intros; rfl
  zpow_neg' (n : â„•) (a : G) : zpow (Int.negSucc n) a = (zpow n.succ a)â»Â¹ := by
    intros; rfl

/--
  `SubNegMonoid.sub'` records what the default definition for `Sub` would be, that
  is `a + -b`, in a class equipped with instances of both `AddMonoid` and `Neg`.
  
  `SubNegMonoid.sub'` exists as a separate definition rather than
  inside `SubNegMonoid` so that the `Sub` field of individual `SubNegMonoid`s
  constructed using that default value will not unfold at `.instance` transparency.
-/
def SubNegMonoid.sub' {G : Type u} [AddMonoid G] [Neg G] (a b : G) : G :=
  a + -b

attribute [to_additive existing SubNegMonoid.sub'] DivInvMonoid.div'

/--
  `SubNegMonoid` is the typeclass for additive monoids with a unary and binary `-`
  operations satisfying `sub_eq_add_neg : âˆ€ a b : G, a - b = a + -b`.

  The default for `sub` is such that `a - b = a + -b` holds by definition.
-/
class SubNegMonoid (G : Type u) extends AddMonoid G, Neg G, Sub G where
  sub := SubNegMonoid.sub'
  sub_eq_add_neg : âˆ€ a b : G, a - b = a + -b := by intros; rfl
  zsmul : â„¤ â†’ G â†’ G := zsmulRec
  zsmul_zero' : âˆ€ a : G, zsmul 0 a = 0 := by intros; rfl
  zsmul_succ' (n : â„•) (a : G) : zsmul (Int.ofNat n.succ) a = a + zsmul (Int.ofNat n) a := by
    intros; rfl
  zsmul_neg' (n : â„•) (a : G) : zsmul (Int.negSucc n) a = -zsmul n.succ a := by
    intros; rfl

attribute [to_additive SubNegMonoid] DivInvMonoid

instance DivInvMonoid.Pow {M} [DivInvMonoid M] : Pow M â„¤ :=
  âŸ¨fun x n â†¦ DivInvMonoid.zpow n xâŸ©

instance SubNegMonoid.SMulInt {M} [SubNegMonoid M] : SMul â„¤ M :=
  âŸ¨SubNegMonoid.zsmulâŸ©

attribute [to_additive existing SubNegMonoid.SMulInt] DivInvMonoid.Pow

/-- `MonoidWithZero` is the typeclass for monoids with zero -/
class MonoidWithZero (M : Type u) extends Monoid M, MulZeroClass M, SemigroupWithZero M


section

protected def Nat.unaryCast {R : Type u} [One R] [Zero R] [Add R] : â„• â†’ R
  | 0 => 0
  | n + 1 => Nat.unaryCast n + 1

/-- `AddMonoidWithOne` is the typeclass for an additive monoid with a `1`. -/
class AddMonoidWithOne (R : Type u) extends NatCast R, AddMonoid R, One R where
  natCast := Nat.unaryCast
  natCast_zero : natCast 0 = 0 := by intros; rfl
  natCast_succ : âˆ€ n, natCast (n + 1) = natCast n + 1 := by intros; rfl

/-- `AddCommMonoidWithOne` is the typeclass for additive monoids `R` with a `1` for
    which `a + b = b + a` for all `a, b : R`. -/
class AddCommMonoidWithOne (R : Type _) extends AddMonoidWithOne R, AddCommMonoid R

end


section MultiplicativeActionByMonoid

/-- `MulAction` is the typeclass for multiplicative actions by monoids. -/
class MulAction (Î± : Type _) (Î² : Type _) [Monoid Î±] extends SMul Î± Î² where
  protected one_smul : âˆ€ b : Î², (1 : Î±) â€¢ b = b
  mul_smul : âˆ€ (x y : Î±) (b : Î²), (x * y) â€¢ b = x â€¢ y â€¢ b

/-- `DistribMulAction` is the typeclass for multiplicative actions by a monoid on
      an additive monoid. -/
class DistribMulAction (M A : Type _) [Monoid M] [AddMonoid A] extends MulAction M A where
  smul_zero : âˆ€ a : M, a â€¢ (0 : A)= 0
  smul_add : âˆ€ (a : M) (x y : A), a â€¢ (x + y) = a â€¢ x + a â€¢ y


section

variable {R M : Type _} [MonoidWithZero R] [Zero M]

variable (R M)

/-- `MulActionWithZero` is the typeclass for multiplicative actions by a monoid
      with zero `R` on a Type with zero `M`, compatible with zero in both `R` and
      `M`. -/
class MulActionWithZero extends MulAction R M where
  smul_zero : âˆ€ r : R, r â€¢ (0 : M) = 0
  zero_smul : âˆ€ m : M, (0 : R) â€¢ m = 0

end


end MultiplicativeActionByMonoid


end Monoid


section Group

/-- `Group` is the typeclass for groups. -/
class Group (G : Type u) extends DivInvMonoid G where
  mul_left_inv : âˆ€ a : G, aâ»Â¹ * a = 1

/-- `AddGroup` is the typeclass for additive groups. -/
class AddGroup (A : Type u) extends SubNegMonoid A where
  add_left_neg : âˆ€ a : A, -a + a = 0

/-- `AddCommGroup` is the typeclass for additive commutative groups. -/
class AddCommGroup (G : Type u) extends AddGroup G, AddCommMonoid G

/-- `CommGroup` is the typeclass for commutative groups. -/
@[to_additive]
class CommGroup (G : Type u) extends Group G, CommMonoid G


section AddGroupWithOne

universe u

protected def Int.castDef {R : Type u} [NatCast R] [Neg R] : â„¤ â†’ R
  | (n : â„•) => n
  | Int.negSucc n => -(n + 1 : â„•)

/-- `AddGroupWithOne` is the typeclass for additive groups with a `1`. -/
class AddGroupWithOne (R : Type u) extends IntCast R, AddMonoidWithOne R, AddGroup R where
  intCast := Int.castDef
  intCast_ofNat : âˆ€ n : â„•, intCast (n : â„•) = Nat.cast n := by intros; rfl
  intCast_negSucc : âˆ€ n : â„•, intCast (Int.negSucc n) = - Nat.cast (n + 1) := by intros; rfl

/-- `AddCommGroupWithOne` is the typeclass for additive commutative groups with a `1`. -/
class AddCommGroupWithOne (R : Type u) extends AddCommGroup R, AddGroupWithOne R, AddCommMonoidWithOne R

end AddGroupWithOne


end Group


section Semiring

class NonUnitalNonAssocSemiring (Î± : Type u) extends AddCommMonoid Î±, Distrib Î±, MulZeroClass Î±

class NonUnitalSemiring (Î± : Type u) extends NonUnitalNonAssocSemiring Î±, SemigroupWithZero Î±

class NonAssocSemiring (Î± : Type u) extends NonUnitalNonAssocSemiring Î±, MulZeroOneClass Î±, AddCommMonoidWithOne Î±

class Semiring (Î± : Type u) extends NonUnitalSemiring Î±, NonAssocSemiring Î±, MonoidWithZero Î±

class CommSemiring (R : Type u) extends Semiring R, CommMonoid R

end Semiring


section Ring

class Ring (R : Type u) extends Semiring R, AddCommGroup R, AddGroupWithOne R

class CommRing (R : Type u) extends Ring R, CommMonoid R


section

open Set

variable {D : Type _}

def Rat.castRec [NatCast D] [IntCast D] [Mul D] [Inv D] : â„š â†’ D
  | âŸ¨a, b, _, _âŸ© => â†‘a * (â†‘b)â»Â¹

def qsmulRec (coe : â„š â†’ D) [Mul D] (a : â„š) (x : D) : D :=
  coe a * x

class DivisionRing (D : Type u) extends Ring D, DivInvMonoid D, Nontrivial D, RatCast D where
  protected mul_inv_cancel : âˆ€ (a : D), a â‰  0 â†’ a * aâ»Â¹ = 1
  protected inv_zero : (0 : D)â»Â¹ = 0
  protected ratCast := Rat.castRec
  protected ratCast_mk : âˆ€ (a : â„¤) (b : â„•) (h1 h2), Rat.cast âŸ¨a, b, h1, h2âŸ© = a * (b : D)â»Â¹ := by
    intros; rfl
  protected qsmul : â„š â†’ D â†’ D := qsmulRec Rat.cast
  protected qsmul_eq_mul' : âˆ€ (a : â„š) (x : D), qsmul a x = Rat.cast a * x := by
    intros; rfl

end


end Ring


section Field

universe u

class Field (K : Type u) extends CommRing K, DivisionRing K

end Field


section VectorSpace

universe u v

variable {F G : Type _}

class VectorSpace (F : Type u) (G : Type v) [Field F] [AddCommGroup G] extends DistribMulAction F G where
  protected add_smul : âˆ€ (r s : F) (x : G), (r + s) â€¢ x = r â€¢ x + s â€¢ x
  protected zero_smul : âˆ€ x : G, (0 : F) â€¢ x = 0


section 

variable [Field ğ”½] [AddCommGroup G] [VectorSpace ğ”½ G] (Î± Î² : ğ”½) (x y : G)

theorem add_smul : (Î± + Î²) â€¢ x = Î± â€¢ x + Î² â€¢ x :=
  VectorSpace.add_smul Î± Î² x

end

end VectorSpace


section IsLinear

universe w

variable {F : Type _} {Gâ‚ : Type _} {Gâ‚‚ : Type _}

class IsLinearMap (F : Type u) (Gâ‚ Gâ‚‚ : Type _) [Field F]
  [AddCommGroup Gâ‚] [AddCommGroup Gâ‚‚] [VectorSpace F Gâ‚] [VectorSpace F Gâ‚‚]
  (f : Gâ‚ â†’ Gâ‚‚) : Prop where
  map_add : âˆ€ x y, f (x + y) = f x + f y
  map_smul : âˆ€ (Î± : F) (x), f (Î± â€¢ x) = Î± â€¢ f x

end IsLinear